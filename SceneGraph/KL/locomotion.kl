
use Math;
use Vec3, Quat, Xfo;
use LinearKeyframe;
use LinearKeyframeTrack;
use LinearKeyframeTrackSet;
use PoseVariables;
use CharacterControllerParams;
use DebugGeometry;

BULLLETWORLD_OPENBLOCKCOMMENT
use FabricBULLET;
use BulletWorld;
BULLLETWORLD_CLOSEBLOCKCOMMENT

function activateClip(
  io LocomotionMarker locomotionMarkers<>[][],
  io TrackSetController trackcontroller,
){
  LocomotionMarker markers[][] = locomotionMarkers[trackcontroller.activeTrackSet];
  LocomotionMarker marker = markers[trackcontroller.pivotFoot][trackcontroller.pivotStepId];
  trackcontroller.time += marker.time;
  trackcontroller.stepIds = marker.stepIds;
  trackcontroller.comParams = marker.params;
  
//  report("activateClip:"+trackcontroller.activeTrackSet+" foot:"+trackcontroller.pivotFoot+ " marker:"+marker);
}


function Scalar calcClipParamsDelta(
  in Scalar clip1[],
  in Scalar clip2[]
){
  Scalar delta = clip1[0] - clip2[0]; // velocity
 // delta += clip1[1] - clip2[1];       // acceleration
  return delta;
}


function addLift(
  io Limb legs[],
  io Integer legId,
  in Scalar lift,
  io CharacterControllerParams controllerparams,
  io Scalar legLiftWeight
){
//  if(leg.hubId < 0 || leg.hubId == hubs.size-1){
    if(legs.size == 2){
      // Only apply the lift from the lowest leg.
      // this prevents overstretching of legs.
      // (important for straight legged creatures like humans).
      if(lift < controllerparams.lift){
        controllerparams.lift = lift;
      }
    }else{
      controllerparams.lift += lift;
    }
    legLiftWeight += 1.0;
 // }
}

BULLLETWORLD_OPENBLOCKCOMMENT
function Boolean fireRay(
  io BulletWorld  bulletworld,
  io Vec3 refPos,
  io Vec3  upvector,
  in Scalar rayLength,
  io Scalar dist,
  io Boolean displayRaycastingDebugging,
  io DebugGeometry debugRaycasting
){
  Vec3 rayStart = refPos + (upvector * rayLength);
  Vec3 rayEnd = refPos - (upvector * rayLength);
  BulletContact contacts[];
  Boolean filterPassiveObjects = false;  
  bulletworld.raycast(rayStart,rayEnd, filterPassiveObjects,contacts);
  if(contacts.size() > 0) {
    Vec3 hitPosition = rayStart + (rayEnd - rayStart) * contacts[0].fraction;
    
    if(displayRaycastingDebugging){
      Xfo tmp;
      tmp.setIdentity();
      tmp.tr = hitPosition;
      debugRaycasting.drawCircle(tmp, 0.03, Color(0.0, 1.0, 1.0, 1.0));
      tmp.tr = refPos;
      debugRaycasting.drawCircle(tmp, 0.05, Color(1.0, 1.0, 1.0, 1.0));
      debugRaycasting.drawLine(rayStart, hitPosition, Color(0.0, 1.0, 1.0, 1.0));
    }
    
    dist = (hitPosition - refPos).dot(upvector);
    return true;
  }
  else{
    dist = 0.0;
    if(displayRaycastingDebugging){
      debugRaycasting.drawLine(rayStart, rayEnd, Color(1.0, 0.0, 0.0, 1.0));
    }
  }
  return  false;
}
BULLLETWORLD_CLOSEBLOCKCOMMENT

operator evaluateLocomotionPoseVariables(
  io Scalar timestep,
  
  io LinearKeyframeTrackSet animationLibrary<>,
  io LocomotionMarker locomotionMarkers<>[][],
  io FootStepTrack footStepTracksLibrary<>[],
  io KeyframeTrackBindings bindingsLibrary<>,
  
  io Hub hubs[],
  io Limb legs[],
  io COM com,
  io LocomotionFoot locomotionFeet[],
  
  io Xfo controllerXfoSet<>,
  io CharacterControllerParams controllerparamsSet<>,
  io Scalar comParamsSet<>[],
  
  io TrackSetController trackcontroller,
  io Boolean plantedFeet[],
  io Vec3 plantLocations[],
  io Vec3 hitPositions[],
  io Vec3 hitPositionsPrevStep[],
  
  io PoseVariables poseVariables,
  io PoseVariables prevUpdatePose,
  io PoseVariables poseError,
  io Scalar clipActivationTime,
  io Scalar minFrequency,
  in Size index,
  
  io DebugGeometry debugFootMotion,
  io DebugGeometry debugRaycastingBULLLETWORLD_OPENBLOCKCOMMENT,
  
  io BulletWorld  bulletworld
  BULLLETWORLD_CLOSEBLOCKCOMMENT
) {
//  report("evaluateLocomotionPoseVariables:"+timestep);
  if(timestep==0.0){
    return;
  }
  debugFootMotion.reset();
  debugRaycasting.reset();
  
  // Constants that mean LLVM optimize our all unused code.
  Boolean displayRaycastingDebugging = ENABLE_DEBUGGING;
  Boolean displayFootPinningDebugging = ENABLE_DEBUGGING;
  Boolean performRaycasting = false;
  BULLLETWORLD_OPENBLOCKCOMMENT
  performRaycasting = true;
  BULLLETWORLD_CLOSEBLOCKCOMMENT
  
  Xfo controllerXfo = controllerXfoSet[index];
  CharacterControllerParams controllerparams = controllerparamsSet[index];
  Scalar comParams[] = comParamsSet[index];
  
  
  plantedFeet.resize(locomotionFeet.size);
  plantLocations.resize(locomotionFeet.size);
  hitPositions.resize(locomotionFeet.size);
  hitPositionsPrevStep.resize(locomotionFeet.size);
  
  /////////////////////////////////////////////////
  // Clip Activation
  if(trackcontroller.deactivate){
    activateClip(
      locomotionMarkers,
      trackcontroller,
    );
  }

  LinearKeyframeTrackSet trackSet = animationLibrary[trackcontroller.activeTrackSet];
  FootStepTrack footStepTracks[] = footStepTracksLibrary[trackcontroller.activeTrackSet];
  KeyframeTrackBindings bindings = bindingsLibrary[trackcontroller.activeTrackSet];
  
  Xfo invcontrollerXfo = controllerXfo.inverse();
  Vec2 timeRange = trackSet.timeRange;
  Scalar t = trackcontroller.time;
  
  Vec3 linearVelocity = controllerparams.displacement.tr / timestep;
  Scalar currSpeed = linearVelocity.length();
  Scalar clipSpeed = trackcontroller.comParams[0];
  if(clipSpeed == 0.0){
    if(trackcontroller.deactivate){
      for(Integer j=0; j < locomotionFeet.size; j++){
        LocomotionFoot foot = locomotionFeet[j];
        Limb leg = legs[foot.limbId];
        plantedFeet[j] = true;
        plantLocations[j] = controllerXfo.transformVector(leg.ikGoalReferenceXfo.tr);
      }
    }
    t += timestep;
    trackcontroller.time = t;
    poseVariables.evaluateKeyframeTracks(t, trackSet, bindings, trackcontroller.currKeys);
    
    /////////////////////////////////////////////////
    // Error dissapation
    if(trackcontroller.tick > 0){
      if(trackcontroller.deactivate){
        poseError = prevUpdatePose.subtract(poseVariables);
        clipActivationTime = trackcontroller.time-timestep;
      }
      if(clipActivationTime > 0.0){
        // Blend off the error over 0.5 seconds. 
        Scalar blendTime = 0.5;
        if(trackcontroller.time - clipActivationTime < blendTime){
          Scalar weight = 1.0 - ((trackcontroller.time - clipActivationTime) / blendTime);
          poseVariables.addWeighted(poseError, weight);
        }
      }
      if(!trackcontroller.deactivate){
        // Hold the feet in place while stationary. 
        for(Integer j=0; j < locomotionFeet.size; j++){
          LocomotionFoot foot = locomotionFeet[j];
          Limb leg = legs[foot.limbId];
        //  poseVariables.xfoValues[leg.ikGoalXfoId].tr = plantLocations[j];
        }
      }
    }
    prevUpdatePose = poseVariables;
    trackcontroller.deactivate = false;
    
    
    for(Integer j=0; j < hubs.size; j++){
      Hub hub = hubs[j];
      Xfo hubXfo = poseVariables.xfoValues[hub.xfoId];
      
      hubXfo.tr = invcontrollerXfo.ori.rotateVector(controllerparams.balanceXfo.ori.rotateVector(controllerXfo.ori.rotateVector(hubXfo.tr))) -
                  invcontrollerXfo.ori.rotateVector(controllerparams.balanceXfo.tr);
      hubXfo.ori =  invcontrollerXfo.ori * controllerparams.balanceXfo.ori * controllerXfo.ori * hubXfo.ori;
      
      poseVariables.xfoValues[hub.xfoId] = hubXfo;
    }
    
    
    if(animationLibrary.size > 1 && currSpeed > 0.001){
      Integer activeTrackSet = trackcontroller.activeTrackSet;
      Scalar targetClipRatio = 0.1;
      Scalar currClipDelta = calcClipParamsDelta(comParams, trackcontroller.comParams);  
      for(Integer clipId=0; clipId<locomotionMarkers.size; clipId++){
      // Note: here we block blending from a clip onto its self, but soon we will enable this.
        if(clipId == activeTrackSet){
          continue;
        }
        for(Integer j=0; j < locomotionFeet.size; j++){
          for(Integer l=0; l < locomotionMarkers[clipId][j].size; l++){
            Scalar clipDelta = calcClipParamsDelta(locomotionMarkers[clipId][j][l].params, trackcontroller.comParams);
            Scalar ratio = currClipDelta / clipDelta;
            
          //  report(activeTrackSet + " to " + clipId + " comParams:"+ comParams  + " clip Params:"+ trackcontroller.comParams + " target Clip Params:"+ locomotionMarkers[clipId][j][l].params + " ratio:"+ ratio);
            if(ratio > targetClipRatio){
            //  report("deactivate from:"+activeTrackSet+ " to:" + clipId);
              trackcontroller.deactivate = true;
              trackcontroller.pivotFoot = j;
              trackcontroller.pivotStepId = l;
              trackcontroller.activeTrackSet = clipId;
              trackcontroller.time = 0.0;
              targetClipRatio = ratio;
            }
          }
        }
      }
      if(trackcontroller.deactivate){
        LocomotionMarker markers[][] = locomotionMarkers[trackcontroller.activeTrackSet];
        LocomotionMarker marker = markers[trackcontroller.pivotFoot][trackcontroller.pivotStepId];
        FootStepTrack targetFootStepTracks[] = footStepTracksLibrary[trackcontroller.activeTrackSet];
        for(Integer j=0; j < locomotionFeet.size; j++){
          FootStepTrack footStepTrack = targetFootStepTracks[j];
          FootStep currStep = footStepTrack.footSteps[marker.stepIds[j]];
          if(marker.time > currStep.liftTime && marker.time < currStep.plantTime ){
            plantedFeet[j] = false;
            plantLocations[j].setNull();
          }
        }
      }
      
    }
          
    if(!trackcontroller.deactivate && trackcontroller.time >= trackSet.timeRange.y){
      trackcontroller.time -= trackSet.timeRange.y;
      trackcontroller.deactivate = true;
    }
  }
  else{
    
    /////////////////////////////////////////////////
    // Clip Frequency and Stride Length calculation
    Scalar stepFrequency;
    Scalar clipFrequencyMultiplier = 1.0;
    Scalar strideLengthMultiplier = 1.0;
    Scalar strideLength = 0.0;
    Scalar gradient = 0.0;
    
    
    clipFrequencyMultiplier = (currSpeed / clipSpeed);
    if(clipFrequencyMultiplier < minFrequency){
      strideLengthMultiplier = clipFrequencyMultiplier / minFrequency;
      clipFrequencyMultiplier = minFrequency;
    }
    
  //  report("currSpeed:"+currSpeed + " clipSpeed:"+clipSpeed + " clipFrequencyMultiplier:"+clipFrequencyMultiplier + " strideLengthMultiplier:"+strideLengthMultiplier);
    // When turning, speed up the playback a little to stop the legs getting tangled. 
    clipFrequencyMultiplier += abs(controllerparams.displacement.ori.getAngle()) * 5.0;
    
    
    /////////////////////////////////////////////////
    // Clip Evaluation
    Scalar timeIncrement = timestep * clipFrequencyMultiplier;
    t += timeIncrement;
    trackcontroller.time = t;
    
    poseVariables.evaluateKeyframeTracks(
      trackcontroller.time,
      trackSet,
      bindings,
      trackcontroller.currKeys
    );
    
    /////////////////////////////////////////////////
    // Now store the com params for the next update.
    for(Integer j=0; j < com.parameterVarIds.size; j++){
      trackcontroller.comParams[j] = poseVariables.scalarValues[com.parameterVarIds[j]];
    }
      
    /////////////////////////////////////////////////
    // Error dissapation
    if(trackcontroller.tick>0){
      if(trackcontroller.deactivate){
        poseError = prevUpdatePose.subtract(poseVariables);
        clipActivationTime = t-timestep;
      }
      if(clipActivationTime > 0.0){
        // Blend off the error across the first half a stride. 
        Scalar blendTime = (clipFrequencyMultiplier * 0.3);
        if(t - clipActivationTime < blendTime){
          Scalar weight = 1.0 - ((t - clipActivationTime) / blendTime);
          poseVariables.addWeighted(poseError, weight);
        }
      }
    }
    prevUpdatePose = poseVariables;
    trackcontroller.deactivate = false;
    
    /////////////////////////////////////////////////
    Xfo rot90;
    rot90.setIdentity();
    rot90.ori.setFromAxisAndAngle(Vec3(0.0, 0.0, 1.0), HALF_PI);
    Vec3 upvector = Vec3(0.0, 1.0, 0.0);//controllerXfo.ori.rotateVector(Vec3(0.0, 1.0, 0.0));
    
    Xfo hubComs[];
    for(Integer j=0; j < hubs.size; j++){
      Hub hub = hubs[j];
      Xfo hubXfo = poseVariables.xfoValues[hub.xfoId];
      
      hubXfo.tr = invcontrollerXfo.ori.rotateVector(controllerparams.balanceXfo.ori.rotateVector(controllerXfo.ori.rotateVector(hubXfo.tr))) -
                  invcontrollerXfo.ori.rotateVector(controllerparams.balanceXfo.tr);
      hubXfo.tr.y -= controllerparams.balanceXfo.tr.length() * 0.5;
      hubXfo.ori =  invcontrollerXfo.ori * controllerparams.balanceXfo.ori * controllerXfo.ori * hubXfo.ori;
      
      if(controllerparams.trail.size > 0){
        hubComs.resize(hubs.size);
        Xfo trailXfo1, trailXfo2;
        Integer id1, id2;
        Xfo lastTrailPoint = controllerparams.trail[controllerparams.trailCircularArrayIndex];
        Scalar trailSegmentDist = (controllerXfo.tr - lastTrailPoint.tr).length();
          
        Scalar trailDistance = hubXfo.tr.z;
      //  report("trailSegmentDist "+j+":"+trailDistance);
        if(trailDistance < 0.0 && hubs.size > 1){
          
          Scalar trailLookup = ((-trailDistance-trailSegmentDist)/controllerparams.trailLength) * Scalar(controllerparams.trail.size);
          Integer trailIndex = Integer(floor(trailLookup));
          Scalar trailInterp = 1.0 - (trailLookup - floor(trailLookup));
          
          id1 = (controllerparams.trailCircularArrayIndex - trailIndex);
          if(id1 < 0){
            id1 += controllerparams.trail.size;
          }
          if(trailIndex < 0){
            trailXfo1 = controllerXfo;
          }else{
            trailXfo1 = controllerparams.trail[id1];
          }
          id2 = (id1 - 1);
          if(id2 < 0){
            id2 += controllerparams.trail.size;
          }
          trailXfo2 = controllerparams.trail[id2];
          
          Xfo trailXfo;
          trailXfo.setIdentity();
          trailXfo.tr = trailXfo2.tr.linearInterpolate(trailXfo1.tr, trailInterp);
          trailXfo.ori = trailXfo2.ori.sphericalLinearInterpolate(trailXfo1.ori, trailInterp);
          
          hubComs[j] = trailXfo;
          hubXfo.tr.z = 0.0;
          hubXfo = invcontrollerXfo * trailXfo * hubXfo;
        }
      }
      
      poseVariables.xfoValues[hub.xfoId] = hubXfo;
    }
    
    /////////////////////////////////////////////////
    trackcontroller.stepIds.resize(locomotionFeet.size);
    
    Scalar appliedComRayDelta = 0.0;
    Scalar appliedFeetRayDeltas[];
    appliedFeetRayDeltas.resize(locomotionFeet.size);
    Scalar legLiftWeight = 0.0;
    
    if(legs.size == 2){
      controllerparams.lift = 99999.0;
    }
      
    for(Integer j=0; j < locomotionFeet.size; j++){
      LocomotionFoot foot = locomotionFeet[j];
      Limb leg = legs[foot.limbId];
      
      FootStepTrack footStepTrack = footStepTracks[j];
      Integer stepId = trackcontroller.stepIds[j];
      
      FootStep currStep = footStepTrack.footSteps[stepId];
      Scalar legSwingRatio = (t - currStep.liftTime) / (currStep.plantTime - currStep.liftTime);
      
      Xfo footMotion = poseVariables.xfoValues[leg.ikGoalXfoId];
      
      Xfo footParentSpaceXfo;
      Vec3 displacementDir;
      if(leg.hubId < 0){
        footParentSpaceXfo = controllerXfo;
        displacementDir = controllerparams.displacementDir;
      }
      else{
        footParentSpaceXfo = controllerXfo * poseVariables.xfoValues[hubs[leg.hubId].xfoId];
        displacementDir = hubComs[leg.hubId].ori.rotateVector(Vec3(0.0,0.0,1.0));
      }
      Xfo footXfo = footParentSpaceXfo * footMotion;
      
      // TODO: this is a bit hackey, and innacurate.
      // One day we should store the plant position
      // relative to the foot positison as an extra track.
      Xfo baseFootXfo = footParentSpaceXfo * leg.ikGoalReferenceXfo;
      if(displayFootPinningDebugging){
        debugFootMotion.drawLine(controllerXfo.tr, baseFootXfo.tr, Color(0.0, 0.5, 0.5, 1.0));
      }
      
      footXfo.tr -= controllerparams.balanceXfo.tr;
      
      Scalar stepValue = poseVariables.scalarValues[foot.stepTimeVarId];
      footXfo.tr += displacementDir * stepValue * strideLengthMultiplier;
      
      Scalar stepRatio = (t - currStep.prevStepMidPlantTime) / (currStep.midPlantTime - currStep.prevStepMidPlantTime);
      Scalar footPlantRatio = stepRatio;
      Vec3 nextStepPos = baseFootXfo.tr - (displacementDir * ((currStep.strideLength * strideLengthMultiplier * footPlantRatio) - (currStep.strideLength * strideLengthMultiplier)));
      
      Scalar footLiftRatio = stepRatio - 0.5;
      if(footLiftRatio < 0.0) footLiftRatio += 1.0;
      Vec3 currStepPos = baseFootXfo.tr - (displacementDir * ((currStep.strideLength * strideLengthMultiplier * footLiftRatio) - (currStep.strideLength * 0.5 * strideLengthMultiplier)));
      
    //  report(j+" footLiftRatio:"+footLiftRatio + " footPlantRatio:"+footPlantRatio);
      if(displayFootPinningDebugging){
        Xfo plantXfo = baseFootXfo;
        plantXfo.tr = nextStepPos;
        debugFootMotion.drawCircle(plantXfo * rot90, 0.2, Color(1.0, 0.5, 0.5, 1.0));
        
        Xfo liftXfo = baseFootXfo;
        liftXfo.tr = currStepPos;
        debugFootMotion.drawCircle(liftXfo * rot90, 0.2, Color(0.5, 0.5, 1.0, 1.0));
      }
    //  report(j+" t:"+t+" stepId:"+stepId +" currStep:"+currStep + " legSwingRatio:"+legSwingRatio + " planted:"+plantedFeet[j]);
      
      /////////////////////////////////////////////////
      // Foot Pinning
      if(displayFootPinningDebugging){
        debugFootMotion.drawCircle(footXfo * rot90, 0.1, Color(0.0, 0.5, 0.5, 1.0));
      }
      Vec3 footError;
      if(legSwingRatio > 0.0 && legSwingRatio < 1.0){
        Scalar liftLift, plantLift;
        if(plantedFeet[j]){
        
          if(displayFootPinningDebugging){
            debugFootMotion.drawLine(currStepPos, plantLocations[j], Color(0.5, 0.5, 0.0, 1.0));
            
            Xfo temp = baseFootXfo;
            temp.tr = plantLocations[j];
            debugFootMotion.drawCircle(temp * rot90, 0.15, Color(0.0, 0.5, 1.0, 1.0));
          }
          
          BULLLETWORLD_OPENBLOCKCOMMENT
          if(performRaycasting){
            Scalar dist;
            if(fireRay(bulletworld, plantLocations[j], upvector, 2.5, dist, displayRaycastingDebugging, debugRaycasting)){
              plantLocations[j] += upvector * dist;
            }
          }
          BULLLETWORLD_CLOSEBLOCKCOMMENT
          footError = invcontrollerXfo.ori.rotateVector(plantLocations[j] - currStepPos);
          plantLocations[j] = footError;
          plantedFeet[j] = false;
          liftLift = footError.y;
          plantLift = 0.0;
        }
        else{
        
          // Interpolate the error across the step.
          if(legSwingRatio < 0.5){
            Scalar errorDissipation = cos(legSwingRatio * TWO_PI) * 0.5 + 0.5;
            footError = plantLocations[j] * errorDissipation;
            
            liftLift = footError.y;
          }
          /////////////////////////////////////////////////
          // Raycasting
          BULLLETWORLD_OPENBLOCKCOMMENT
          if(performRaycasting){
            Scalar dist;
            if(fireRay(bulletworld, nextStepPos, upvector, 2.5, dist, displayRaycastingDebugging, debugRaycasting)){
              Scalar lift = dist * legSwingRatio;
              footError.y += lift;
              plantLift = lift;
              hitPositionsPrevStep[j] = nextStepPos + (upvector*dist);
            }
          }
          BULLLETWORLD_CLOSEBLOCKCOMMENT
        }
        if(performRaycasting){
          addLift(legs, j, liftLift+plantLift, controllerparams, legLiftWeight);
        }
      }
      else{
        if(performRaycasting){
          // Recalculate the lift each update even when the foot is planted,
          // because we also transfer this lift onto the controller for the
          // next update.
          
          if(displayRaycastingDebugging){
            Xfo tmp = footXfo;
            tmp.tr = hitPositionsPrevStep[j];
            debugRaycasting.drawCircle(tmp * rot90, 0.03, Color(0.0, 1.0, 1.0, 1.0));
            tmp.tr = currStepPos;
            debugRaycasting.drawCircle(tmp * rot90, 0.05, Color(1.0, 1.0, 1.0, 1.0));
            debugRaycasting.drawLine(currStepPos, hitPositionsPrevStep[j], Color(0.0, 1.0, 1.0, 1.0));
          }
              
          Scalar lift = (hitPositionsPrevStep[j] - currStepPos).dot(upvector);
          footXfo.tr += Vec3(0.0, lift, 0.0);
          addLift(legs, j, lift, controllerparams, legLiftWeight);
        }
        if(!plantedFeet[j]){
          // Record the foot plant pos.
          plantLocations[j] = currStepPos;
          plantedFeet[j] = true;
        }
        else{
          // Calculate the error each update to hold the foot in place while planted.
          footError = invcontrollerXfo.ori.rotateVector(plantLocations[j] - currStepPos);
          // We zero the error vector here, because the raycasting (above) handles the y axis.
          footError.y = 0.0;
        }
        
        if(displayFootPinningDebugging){
          debugFootMotion.drawCircle(footXfo * rot90, 0.1, Color(0.5, 0.5, 0.0, 1.0));
          debugFootMotion.drawLine(currStepPos, plantLocations[j], Color(0.5, 0.5, 0.0, 1.0));
          
          Xfo temp = baseFootXfo;
          temp.tr = plantLocations[j];
          debugFootMotion.drawCircle(temp * rot90, 0.15, Color(0.0, 1.0, 0.5, 1.0));
        }
        
        if(t >= currStep.midPlantTime){
          /////////////////////////////////////////////////
          // Clip Deactivation
          if(animationLibrary.size > 1){
            Scalar targetClipRatio = 0.55;
            Integer activeTrackSet = trackcontroller.activeTrackSet;
            Scalar currClipDelta = calcClipParamsDelta(comParams, trackcontroller.comParams);
            for(Integer clipId=0; !trackcontroller.deactivate && clipId<locomotionMarkers.size; clipId++){
              // Note: here we block blending from a clip onto its self, but soon we will enable this.
              if(clipId == activeTrackSet){
                continue;
              }
              for(Integer l=0; l<locomotionMarkers[clipId][j].size; l++){
                Scalar clipDelta = calcClipParamsDelta(locomotionMarkers[clipId][j][l].params, trackcontroller.comParams);
                Scalar ratio = currClipDelta / clipDelta;
              //  report(activeTrackSet + " to " + clipId + " comParams:"+ comParams  + " clip Params:"+ trackcontroller.comParams + " target Clip Params:"+ locomotionMarkers[clipId][j][l].params + " ratio:"+ ratio);
                if(ratio > targetClipRatio){
                //  report("deactivate from:"+activeTrackSet+ " to:" + clipId);
                  trackcontroller.deactivate = true;
                  trackcontroller.pivotFoot = j;
                  trackcontroller.pivotStepId = l;
                  trackcontroller.activeTrackSet = clipId;
                  trackcontroller.time = t - currStep.midPlantTime;
                  targetClipRatio = ratio;
                }
              }
            }
          }
          
          if(!trackcontroller.deactivate && currStep.midPlantTime >= timeRange.y){  
            // Deactivate the current clip if a foot reaches the last step.
          //  report("deactivate - end of clip foot:"+j+ " from t:" + trackcontroller.time + " to t:"+(trackcontroller.time - currStep.midPlantTime));
            trackcontroller.deactivate = true;
            trackcontroller.pivotFoot = j;
            trackcontroller.pivotStepId = 0;
            trackcontroller.time -= currStep.midPlantTime;
          }
          
          if(!trackcontroller.deactivate){
            trackcontroller.stepIds[j]++;
          }
        }
      }
      
      if(displayRaycastingDebugging){
        debugFootMotion.drawCircle(footXfo * rot90, 0.04, Color(0.5, 1.0, 0.0, 1.0));
      }
        
      footXfo = invcontrollerXfo * footXfo;
      // Apply the error in COM space. 
      footXfo.tr += footError;
      
      poseVariables.xfoValues[leg.ikGoalXfoId] = footXfo;
      
    }
    if(performRaycasting && legLiftWeight > 0.0){
    
      if(legs.size != 2){
        controllerparams.lift /= legLiftWeight;
      }
      
      if(displayRaycastingDebugging){
        debugRaycasting.drawLine(controllerXfo.tr, controllerXfo.tr + (upvector * controllerparams.lift), Color(0.0, 1.0, 1.0, 1.0));
      }
              
      // Smooth out the com motion by scalaing the lift parameter.
      // Slightly hackey. The applied lift could be calculated better instead of this.
      controllerparams.lift *= 0.4;
      // Note: this is highly illegal. We are copying data back to one of our dependencies.
      // This will be eliminated with nested sub-graphs.
      controllerparamsSet[index] = controllerparams;
    }
  }
  trackcontroller.tick++;  
}


