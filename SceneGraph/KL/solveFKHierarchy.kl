//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

use Xfo, Bone;

operator solveFKBone(
  io Xfo pose[],
  io Bone bones[],
  io PoseParameterBinding binding,
  io PoseVariables poseVariables
) {
  if (bones[binding.target].parent == - 1) {
    pose[binding.target] = poseVariables.xfoValues[binding.target];
  }
  else {
    pose[binding.target] = pose[bones[binding.target].parent] * poseVariables.xfoValues[binding.source];
  }
}

operator solveFKHierarchy(
  io Xfo pose[],
  io Bone bones[],
  io PoseParameterBinding bindings[],
  io PoseVariables poseVariables
) {
  for (var Integer i = 0; i < bindings.size(); i++) {
    if (bones[bindings[i].target].parent == - 1) {
      pose[bindings[i].target] = poseVariables.xfoValues[i];
    }
    else {
      pose[bindings[i].target] = pose[bones[bindings[i].target].parent] * poseVariables.xfoValues[bindings[i].source];
    }
  }
}

operator invertFKHierarchy(
  io Xfo pose[],
  io Bone bones[],
  io PoseParameterBinding bindings[],
  io PoseVariables poseVariables
) {
  for (var Integer i = 0; i < bindings.size(); i++) {
    if (bones[bindings[i].target].parent == - 1)
      poseVariables.xfoValues[bindings[i].source] = pose[bindings[i].target];
    else
      poseVariables.xfoValues[bindings[i].source] = pose[bones[bindings[i].target].parent].inverse() * pose[bindings[i].target];
  }
}

