
//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

use Vec2;

function LinearKeyframe(Scalar time, Scalar value) {
  this.time = time;
  this.value = value;
}


function Scalar LinearKeyframe.interpolate(LinearKeyframe key2, Scalar t) {
  Scalar u = (t - this.time) / (key2.time - this.time);
  return this.value + ((key2.value - this.value) * u);
}


function LinearKeyframeTrack.clear() {
  this.keys.resize(0);
}


function Scalar LinearKeyframeTrack.evaluate(
  in Scalar time,
  io Integer keyIndex
) {
  Integer numKeys = this.keys.size;
  if(numKeys == 0){
    return 0.0;
  }
  else if(numKeys == 1){
    return this.keys[0].value;
  }
  else if (time >= this.keys[numKeys - 1].time) {
    return this.keys[numKeys - 1].value;
  }
  else if (time <= this.keys[0].time) {
    return this.keys[0].value;
  }
  else {
    if (this.keys[keyIndex].time <= time && time <= this.keys[keyIndex + 1].time) {
      // we are still on the same segment as the previous evaluation.
    }
    else if (this.keys[keyIndex + 1].time < time && time < this.keys[keyIndex + 2].time) {
      keyIndex++;
    }
    else {
      for (var Integer i = 0; i < numKeys - 1; i++) {
        if (this.keys[i].time <= time && time <= this.keys[i + 1].time) {
          keyIndex = i;
          break;
        }
      }
    }
    return this.keys[keyIndex].interpolate(this.keys[keyIndex + 1], time);
  }
}


function LinearKeyframeTrack.setValue(
  in Scalar time,
  in Scalar value
) {
  Integer numKeys = this.keys.size;
  if (numKeys == 0 || time > this.keys[numKeys - 1].time) {
    this.keys.push(LinearKeyframe(time, value));
  }
  else {
    Integer keyIndex = -1;
    for (var Integer i = 0; i < numKeys; i++) {
      if (this.keys[i].time == time) {
        keyIndex = i;
        break;
      }
    }
    if(keyIndex != -1){
      this.keys[keyIndex].value = value;
    }
    else{
      // This ensures that the last key always has the greatest time value.
      // this optimizes the common case where we are incrementaly keying 
      // forwards through time. 
      this.keys.push(this.keys[numKeys - 1]);
      this.keys[numKeys - 1] = LinearKeyframe(time, value);
      // needs sorting == true
    }
  }
}
function Vec2 LinearKeyframeTrackSet.getTimeRange() {
  return this.timeRange;
}


function LinearKeyframeTrackSet.calcTimeRange() {
  this.timeRange.set(0.0, 0.0);
  for (Integer i = 0; i < this.tracks.size; i++) {
    if(this.tracks[i].keys.size == 0){
      continue;
    }
    if(i==0){
      this.timeRange.x = this.tracks[i].keys[0].time;
      this.timeRange.y = this.tracks[i].keys[this.tracks[i].keys.size-1].time;
    }
    else{
      if(this.timeRange.x > this.tracks[i].keys[0].time){
        this.timeRange.x = this.tracks[i].keys[0].time;
      }
      if(this.timeRange.y < this.tracks[i].keys[this.tracks[i].keys.size-1].time){
        this.timeRange.y = this.tracks[i].keys[this.tracks[i].keys.size-1].time;
      }
    }
  }
}


function LinearKeyframeTrackSet.clearTracks(
  Integer trackIds[]
) {
  for (Integer i = 0; i < trackIds.size; i++) {
    this.tracks[trackIds[i]].clear();
  }
}


function LinearKeyframeTrackSet.setValues(
  io PoseVariables values,
  in Scalar time,
  io KeyframeTrackBindings bindings
) {
  for (Integer i = 0; i < bindings.scalarBindings.size; i++) {
    KeyframeTrackBinding binding = bindings.scalarBindings[i];
    if(binding.trackIds.size == 1){
      Scalar val = values.scalarValues[binding.varId];
      this.tracks[binding.trackIds[0]].setValue(time, val);
    }
  }
  
  for (Integer i = 0; i < bindings.quatBindings.size; i++) {
    KeyframeTrackBinding binding = bindings.quatBindings[i];
    if(binding.trackIds.size == 3){
      //  Quat to Euler
      report("TODO:Euler.setFromQuat");
    //  Euler euler;
    //  euler.setFromQuat(values.quatValues[binding.varId]);
    //  this.tracks[binding.trackIds[0]].setValue(time, euler.x),
    //  this.tracks[binding.trackIds[1]].setValue(time, euler.y),
    //  this.tracks[binding.trackIds[2]].setValue(time, euler.z)));
    }
    else if(binding.trackIds.size == 4){
      Quat q = values.quatValues[i];
      this.tracks[binding.trackIds[0]].setValue(time, q.v.x );
      this.tracks[binding.trackIds[1]].setValue(time, q.v.y );
      this.tracks[binding.trackIds[2]].setValue(time, q.v.z );
      this.tracks[binding.trackIds[3]].setValue(time, q.w );
    }
  }
  
  for (Integer i = 0; i < bindings.xfoBindings.size; i++) {
    KeyframeTrackBinding binding = bindings.xfoBindings[i];
    if(binding.trackIds.size != 7){
      report('incorrect binding for Xfo:' + binding);
      continue;
    }
    Xfo xfo = values.xfoValues[binding.varId];
    
    this.tracks[binding.trackIds[0]].setValue(time, xfo.tr.x);
    this.tracks[binding.trackIds[1]].setValue(time, xfo.tr.y);
    this.tracks[binding.trackIds[2]].setValue(time, xfo.tr.z);
    
    if(binding.trackIds.size == 6){
      //  Quat to Euler
      report("TODO:Euler.setFromQuat");
    //  Euler euler;
    //  euler.setFromQuat(xfo.ori);
    //  this.tracks[binding.trackIds[0]].setValue(time, euler.x),
    //  this.tracks[binding.trackIds[1]].setValue(time, euler.y),
    //  this.tracks[binding.trackIds[2]].setValue(time, euler.z)));
    }else if(binding.trackIds.size == 7){
      this.tracks[binding.trackIds[3]].setValue(time, xfo.ori.v.x);
      this.tracks[binding.trackIds[4]].setValue(time, xfo.ori.v.y);
      this.tracks[binding.trackIds[5]].setValue(time, xfo.ori.v.z);
      this.tracks[binding.trackIds[6]].setValue(time, xfo.ori.w);
    }
  }
}
