#
# Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
#

import itertools, os

Import( 'baseEnv', 'buildOS' )

if 'dist' in COMMAND_LINE_TARGETS:
  Import( 'klToolInstaller', 'fileSystemInstaller' )
  installers = [klToolInstaller, fileSystemInstaller]
  if buildOS == 'Windows':
    Import( 'kinectInstaller' )
    installers.append( kinectInstaller )
  installers += SConscript( 'Chrome/SConscript' )
  installers += SConscript( 'Firefox/SConscript' )
  installers += SConscript( 'Python/SConscript' )
  if buildOS != 'Windows':
    installers += SConscript( 'Node/SConscript' )
  Alias( 'dist', installers )
  if 'upload' in COMMAND_LINE_TARGETS:
    if buildOS == 'Windows':
      unixInstallers = ['/c'+'/'.join(str(item).split('\\'))[2:] for item in installers]
      uploadCommand = [ 'scp', '-i', os.environ['HOME'] + '/.ssh/id_rsa' ]
      uploadCommand += unixInstallers
      uploadCommand += [ 'fabric-engine.com:' ]
    else:
      uploadCommand = [ 'rsync', '-e', 'ssh', '--progress', '$SOURCES', 'fabric-engine.com:' ]
    upload = baseEnv.AlwaysBuild( baseEnv.Command(
      'upload',
      installers,
      [uploadCommand]
      ) )
    Alias( 'upload', upload )
